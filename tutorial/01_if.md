# Comparison functions

In stekk, a non-empty container or a non-zero number plays a role of a `truthy` 

# Conditional expressions

Conditional expression syntax:

```html
if <condition> <then_expreession> else <else_expression>
```

Unlike most imperative languages, there is no conditional *statement* or
*instruction* in stekk -- only conditional *expressions*.

`condition` is considered either "true" or "false" according to these rules:
1. `0` and `0.0` are `false`
2. `$N` (null), `$E` (error), `$T` (type mismatching) are `false`
3. empty string `""` and empty list `[]` are `false`
4. everything else is `true`

## Examples

```
[stekk v0.0.1] if 1 "true" else "false"
true
```

```
[stekk v0.0.1] if 0 "true" else "false"
false
```

Of course, the condition can be a compound expression. You can also use the
fact that stekk pretty much ignores whitespace to format things nicely.

```
[stekk v0.0.1] if (3 6 .*)
...                "Hello, world!" 
...            else
...                "Lorem ipsum."
Hello, world!
```

## Comparison functions

Comparison functions take two parameters and return either `1` or `0`.

```
>   greater than
>=  greater than or equals
<   less than
<=  less than or equals
=   equals
!=  doesn't equal
```

### Examples

```
[stekk v0.0.1] x := 5;
[stekk v0.0.1] y := 3;
[stekk v0.0.1] if (x y .>)
...                "x is bigger!"
...            else
...                "x is not bigger..."
x is bigger!
```


`$T` 
```
[stekk v0.0.1] eight := (3 "five" .+)
[stekk v0.0.1] if (eight $T .=)
...                "oops..."
...            else
...                "alright!"
oops...
```

# Conditional statements

> Wait, you said there were no conditional statements in stekk!

There aren't any special conditional statements in stekk. However, any 
expression can also be used as a statement. This is a correct program:

```
1;
2;
3;
4;
"Hello, world";

```

This might seem useless, but it can be very useful if the expression has a side
effect:

```
greet := {
    ("Hello, world!" .println 42);
};

.greet;     ;; this is an expression which is used as a statement
            ;; its result is discarded, however, it produces a side effect
```

This program could be rewritten as:

```
.{
    ("Hello, world!" .println 42);
};
```
Here a code block is created and then called -- that's an expression which is a
function call.

You can use this to your advantage and create conditional statements:

```
[stekk v0.0.1] x := 5;
[stekk v0.0.1] if (x 4 .>) .{
...                x := 1000;
...            } else .{
...                x := 0;
...            }
[stekk v0.0.1] x
1000
```

This little program translates to this:
```
Evaluate this expression:
    if (x > 4), evaluate this expression:
        (function call: {x := 1000})
    else, evaluate this expression:
        (function call: {x := 0})
```

Here is an equivalent (considering the result) program:

```
[stekk v0.0.1] x := 5
[stekk v0.0.1] . if(x 4 .>) {
...                x := 1000;
...            } else {
...                x := 0;
...            }
[stekk v0.0.1] x
1000
```
Here you're calling a conditional expression:

```
call a function, where "function" is:
    {x := 1000} if (x > 4)
    {x := 0}    otherwise
```

Considering the fact that you can store functions or code blocks in variables,
you could have also written the program as:
```
[stekk v0.0.1] x := 5;
[stekk v0.0.1] action := if (x 4 .>) { x := 1000; } else { x := 0; }
[stekk v0.0.1] .action
[stekk v0.0.1] x
1000
```
If you're having trouble understanding this, run this and try to understand what
is going on:

```
[stekk v0.0.1] x := 5
[stekk v0.0.1] action := if (x 4 .>) { x := 1000; } else { x := 0; }
[stekk v0.0.1] action
{Assign(LvalueName(x))=(1000)}
[stekk v0.0.1] 
[stekk v0.0.1] x := 3
[stekk v0.0.1] action := if (x 4 .>) { x := 1000; } else { x := 0; }
[stekk v0.0.1] action                                               
{Assign(LvalueName(x))=(0)}
```

The `if expression` will choose between two *code blocks*, and either one of
them will be stored in the `action` variable.

Then, you can call `action`, and that will run all the statements inside the
code block which is stored inside the `action` variable.
